#compdef genzshcomp EXAMPLE_COMMAND
# your Python script command name add to compdef.

_func() {
    _arguments -s -S \
        ":::_get_helplist" \
        "*::args:_files"
}

(( $+functions[_get_helplist] )) ||
_get_helplist() {
    local opts
    local cache_policy
    local cache_name="${service}_options"
    zstyle -s ":completion:${curcontext}:" cache-policy cache_policy
    if [[ -z "$cache_policy" ]]; then
        zstyle ":completion:${curcontext}:" cache-policy _opts_caching_policy
    fi

    if ( [[ ${+_opts} -eq 0 ]] || _cache_invalid ${cache_name} ) \
        && ! _retrieve_cache ${cache_name}; then
        opts=(${${(f)"$(${service} --help | genzshcomp -f list)"}})
        _store_cache ${cache_name} opts
    fi

    _describe 'options' opts
}

_opts_caching_policy() {
    local -a oldp
    oldp=( "$1"(Nmw+1) )
    (( $#oldp ))
}

_func "$@"
